<myhtml> ::= <start_MYHTML> <head>? <body> <end_MYHTML>

<start_MYHTML> ::= "<MYHTML>"
<end_MYHTML> ::= "</MYHTML>"

<head> ::= <start_head> <title> <meta>* <end_head>
<start_head> ::= "<head>"
<end_head> ::= "</head>"

<title> ::= "<title>" <text> "</title>"
<meta> ::= "<meta" <meta_attr_group> "/>"
<meta_attr_group> ::= "charset=" <quoted_text>
                   | "name=" <quoted_text> " content=" <quoted_text>

<body> ::= <start_body> (<body_elements> | <comment>)* <end_body>
<start_body> ::= "<body>"
<end_body> ::= "</body>"

<body_elements> ::= <p> | <a> | <img> | <form> | <div>

<p> ::= "<p" <style_attr>? ">" (<text> | <comment>)* "</p>"
<a> ::= "<a href=" <quoted_text> ">" (<img>? <text>? | <text>? <img>?) "</a>"
<img> ::= "<img" <img_attr_group> "/>"
<img_attr_group> ::= "src=" <quoted_text> " alt=" <quoted_text> (<img_opt_attr>)*
<img_opt_attr> ::= " width=" <positive_int> | " height=" <positive_int>

<form> ::= "<form" <style_attr>? ">" (<input> | <label>)+ "</form>"
<input> ::= "<input type=" <quoted_text> (<input_opt_attr>)* "/>"
<input_opt_attr> ::= " value=" <quoted_text> | " style=" <quoted_text>

<label> ::= "<label for=" <quoted_text> <style_attr>? ">" (<text> | <comment>)* "</label>"
<div> ::= "<div" <style_attr>? ">" (<body_elements> | <comment>)* "</div>"

<style_attr> ::= "style=" <quoted_text>
<quoted_text> ::= '"' <text> '"' | '\'' <text> '\''
<text> ::= (<text_data> | <comment>)*
<text_data> ::= [any valid character data excluding '<' and '>']
<positive_int> ::= [0-9]+

<comment> ::= "<!--" <comment_text> "-->"
<comment_text> ::= [any sequence of characters excluding "--" not followed by ">"]